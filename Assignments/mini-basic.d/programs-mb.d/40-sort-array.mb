# $Id: 40-sort-array.mb,v 1.3 2021-04-14 08:47:59-07 - - $
# Sort numbers.
# Input is a sequence of numbers ending with end of file.
# User is assumed to have not more than 100 numbers.
# Note that nan != nan, other was x = x for all x that is not nan.
# Algorithm: bubble sort O(n^2).
        let size = 100
        dim a[size]
        let max = 0
read:   input x
        if eof > 0 goto eof
        if x != x goto error
        let a[max] = x
        let max = max + 1
        if max < size goto read
eof:
        print ""
        print "unsorted"
        let i = 0
prtlp:  print "a[", i, "]=", a[i]
        let i = i + 1
        if i < max goto prtlp
        if i < 1 goto sorted

        let i = max - 1
outer:  let j = 0 
inner:  if a[j] <= a[j + 1] goto noswap
        let t = a[j]
        let a[j] = a[j+1]
        let a[j+1]=t
noswap: let j = j + 1
        if j <= i - 1 goto inner
        let i = i - 1
        if i > 0 goto outer

sorted: print ""
        print "sorted"
        let i = 0
sortlp: print "a[", i, "]=", a[i]
        let i = i + 1
        if i < max goto sortlp
        goto stop
error:  print "Invalid input"
stop:
